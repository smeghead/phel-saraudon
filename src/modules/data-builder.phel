(ns saraudon\modules\data-builder
  (:require phel\str :as s)
  (:require saraudon\modules\types))

(defstruct temporary-node [path modify-lines])

(defstruct node [id path label modify-lines])

(defstruct link [source target value])

(defstruct data [nodes links])

(defn- unique-node [elements aditionals]
  (loop [elements elements
         aditionals aditionals]
    (if (empty? aditionals)
      elements
      (let [element (first aditionals)
            new? (empty? (find |(if (empty? $)
                                  false
                                  (= ($ :path) (element :path))) elements))]
            # avoid find problem. #737
            #new? (empty? (find |(= ($ :path) (element :path)) elements))]
        (recur (if new?
                 (push elements element)
                 elements)
               (rest aditionals))))))

(defn- get-nodes [commits]
  (loop [commits commits
         acc []]
    (if (empty? commits)
      (loop [t-nodes acc
             id 0
             acc2 []]
        (if (empty? t-nodes)
          acc2
          (let [t-node (first t-nodes)
                path (t-node :path)
                label (s/replace path "/^.*\/([^\/]*)$/" "$1")]
            (recur (rest t-nodes) (inc id) (push acc2 (node id path label 0))))))
      (recur (rest commits) (unique-node acc (map |(temporary-node ($ :path) ($ :modify-lines)) ((first commits) :files)))))))

(defn- unique-link [elements aditionals]
  (loop [elements elements
         aditionals aditionals]
    (if (empty? aditionals)
      elements
      (let [element (first aditionals)
            new? (empty? (find |(if (empty? $)
                                  false
                                  (or (and (= ($ :source) (element :source))
                                           (= ($ :target) (element :target)))
                                      (and (= ($ :source) (element :target))
                                           (= ($ :target) (element :source)))))
                               elements))]
            # avoid find problem. #737
            #new? (empty? (find |(= ($ :id) (element :id)) elements))]
        (recur (if new?
                 (push elements element)
                 elements)
               (rest aditionals))))))

(defn- get-node-index [nodes file]
  (loop [nodes nodes]
    (let [node (first nodes)]
      (if (= (node :path) (file :path))
        (node :id)
        (recur (rest nodes))))))

(defn- get-links [commits nodes]
  (loop [commits commits
         acc []]
    (if (empty? commits)
      acc
      (let [new-links (filter truthy? (for [source :in ((first commits) :files)
                                            target :in ((first commits) :files)]
                                        (if (= (source :path) (target :path))
                                          nil
                                          (link (get-node-index nodes source) (get-node-index nodes target) 1))))]
        (recur (rest commits) (unique-link acc new-links))))))

(defn build [commits]
  (let [nodes (get-nodes commits)
        links (get-links commits nodes)]
    (data nodes links)))
